buildscript {
    ext {
        kotlin_version = '1.7.10'
    }
    repositories {
        mavenCentral()
        gradlePluginPortal()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "com.github.ben-manes:gradle-versions-plugin:0.42.0"
    }
}

apply plugin: 'kotlin'
apply plugin: "com.github.ben-manes.versions"

group 'com.chaomao.functions'
version '1.0'

repositories {
    mavenCentral()
}

sourceCompatibility = JavaVersion.VERSION_17
compileKotlin {
    kotlinOptions.jvmTarget = JavaVersion.VERSION_17
}

compileTestKotlin {
    kotlinOptions.jvmTarget = JavaVersion.VERSION_17
}

configurations {
    invoker
}

dependencies {
    implementation "com.google.cloud.functions:functions-framework-api:1.0.4"
    implementation "com.google.cloud:google-cloud-firestore:3.3.0"
    invoker "com.google.cloud.functions.invoker:java-function-invoker:1.1.0"
}

tasks.register("runFunction", JavaExec) {
    main = 'com.google.cloud.functions.invoker.runner.Invoker'
    classpath(configurations.invoker)
    inputs.files(configurations.runtimeClasspath, sourceSets.main.output)
    args(
        '--target', project.findProperty('run.functionTarget') ?: "functions.HelloWorld",
        '--port', project.findProperty('run.port') ?: 8080
    )
    doFirst {
        args('--classpath', files(configurations.runtimeClasspath, sourceSets.main.output).asPath)
    }
}

archivesBaseName = 'app'
